buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.tweaker-client'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'java'

version = "1.0.0"
group= "com.evieclient" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "EvieClient"

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

minecraft {
    version = "1.8.9"
    tweakClass = "com.evieclient.mixins.EvieTweaker"
    runDir = "run"
    mappings = "stable_22"
    clientJvmArgs = ["-XX:-DisableExplicitGC"]
    makeObfSourceJar = false
    clientRunArgs = findProperty("clientArgs")?.split() ?: []
}

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

runClient {
    outputs.upToDateWhen { false }
}

repositories {
    maven { url "https://jitpack.io" }
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven/'
    }
    mavenCentral()
    jcenter()

}

configurations {
    embed
    compile.extendsFrom(embed)
}

dependencies {
    // EvieClient Deps //

    // Analytics
    embed 'io.sentry:sentry:5.6.0'

    // Utils
    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'

    embed 'com.google.code.gson:gson:2.8.5'
    embed 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.61'
    embed 'com.github.mew:Slothpixel-JVM:master-SNAPSHOT'
    embed 'com.github.Vatuu:discord-rpc:1.6.2'
    embed group: 'co.gongzh.procbridge', name: 'procbridge', version: '1.1.1'

    // Mixin/Minecraft Deps //

    annotationProcessor 'org.spongepowered:mixin:0.7.11-SNAPSHOT'
    embed('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
    }

    embed 'com.github.FalseHonesty:reflections:855f40b80b'
    embed 'com.github.ben-manes.caffeine:caffeine:2.8.0'

    embed ('org.slick2d:slick2d-core:1.0.2') {
        exclude module: 'lwjgl'
    }

    embed ("com.github.KevinPriv:MojangAPI:1.0") {
        exclude module: "gson"
    }

    embed 'com.github.Cubxity:DeobfStack:1.2'

    embed 'org.ow2.asm:asm:5.0.3'

    embed fileTree(dir: 'libs', include: '*.jar')
}

task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.runtime
}

mixin {
    defaultObfuscationEnv notch
    add sourceSets.main, "mixin.evieclient.refmap.json"
}

processResources {
    inputs.files "src/main/resources"
    outputs.dir "build/classes/main"
    copy {
        from("src/main/resources")
        into("build/classes/main")
    }
}

jar {
    dependsOn configurations.compile
    from {
        configurations.embed.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}

jar {
    manifest.attributes(
            "MixinConfigs": 'mixins.evieclient.json',
            "TweakClass": 'com.evieclient.mixins.EvieTweaker',
            "TweakOrder": 0,
            "Manifest-Version": 1.0
    )

    configurations.embed.each { dep ->
        from(project.zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
            exclude 'org/jetbrains/annotations', 'org/jetbrains/annotations/**'
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.10.3'
}
